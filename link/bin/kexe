#!/bin/zsh
# Author            : Hongbo Liu <hbliu@freewheel.tv>
# Date              : 2017-12-05
# Last Modified Date: 2017-12-05
# Last Modified By  : Hongbo Liu <hbliu@freewheel.tv>

usage() {
cat << EOF
Usage: $(basename $0)  pod_name_part command

The pod_name could only be part of the real pod name.

Options:
-h  print usage
-c  specify container name
-n  specify namespace
EOF
exit 0
}

get_real_podname() {
    local opt_ns=$1
    local pod_name=$2
    echo "$(kubectl get pod $opt_ns -o name | grep -E "${pod_name}" | cut -d '/' -f2-)"
}

main() {
    zparseopts -D -E -M -A Args -- n: h -namespace:=n c: -container:=c

    local pod_name="${1:-aws-ads}"
    local shell="${2:-bash}"
    local opt_ns=""

    if (( ${+Args[-h]} )); then
        usage
    fi

    if (( ${+Args[-n]} )); then
        opt_ns="-n${Args[-n]}"
    fi

    local opt_container=
    if (( ${+Args[-c]} )); then
        opt_container="-c${Args[-c]}"
    fi

    local name="$(get_real_podname "$opt_ns" $pod_name)"
    local cnt=$(echo $name | wc -l)

    if [[ "$cnt" -eq 0 ]]; then
        echo "Error: could not get matched pod"
        exit 1
    elif [[ "$cnt" -gt 1 ]]; then
        echo -e "There are multiple pods, please use more specific name:\n"
        echo $name
        exit 2
    fi

    kubectl exec $opt_ns $opt_container -it $name $shell
}

main $@
